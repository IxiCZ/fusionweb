package cz.ixi.fusionweb.drools.rules;


rule "Insert searched text event"
when
	ProductSearchUnsuccessfulEvent($searchedText : searchedText)
	not SearchedText(text == $searchedText)
then
	insert(new SearchedText( $searchedText));
end

rule "Retract searched text event"
when

	$searched : SearchedText($text : text)
	not ProductSearchUnsuccessfulEvent(searchedText == $text)
then
	retract($searched);
end


rule "Create notification if too many unsuccessful seach events"
no-loop
when
	$searchedText: SearchedText($text: text)
	not SearchedTextReported(text == $text) over window:time(1h)
	ArrayList(size >= 10) from collect( ProductSearchUnsuccessfulEvent( searchedText == $text) over window:time(1h))
then
	channels["productSearchUnsuccessful"].send($text);
	insert(new SearchedTextReported($text));
end


rule "Search text reported"
when
  SearchedTextReported() over window:time(1h)
then
end
