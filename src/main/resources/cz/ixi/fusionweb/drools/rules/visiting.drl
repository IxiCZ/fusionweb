package cz.ixi.fusionweb.drools.rules;

rule "Create notification if there is visited, but not bought product."
no-loop
when
	ProductNavigationEvent($productId: productId, $productName: productName) 
	not ProductVisitedButNotBoughtReported(productId == $productId) over window:time(1h)
	$visited: Number() from accumulate( $pne: ProductNavigationEvent(productId == $productId) over window:time(1h),  count( $pne ) )
	Number( $visited - intValue >= 100 ) from accumulate( $pbe: ProductBoughtEvent(productId == $productId) over window:time(1h),  count( $pbe ) )

then
	insert(new ProductVisitedButNotBoughtReported($productId));
	channels["notificationsGeneral"].send(new Notification(NotificationSeverity.SEVERE, new Date(), 
	"In the last hour was product "+ $productId +":" +$productName + " been visited many times, but not bought a lot." ));
end

rule "Product Visited But Not Bought Reported"
when
  ProductVisitedButNotBoughtReported() over window:time(1h)
then
end


rule "Report no visited products in the last hour."
timer (cron:0 0 */1 * * ?)
no-loop
when
	not (ProductNavigationEvent() over window:time(1h))
then
	channels["statisticsHourly"].send("No product was visited in the last hour." );
end

rule "Report most visited product in the last hour if any."
no-loop
timer (cron:0 0 */1 * * ?)
when
	exists (ProductNavigationEvent() over window:time(1h))
	$m: Number() from accumulate ($pne : ProductNavigationEvent() over window:time(1h), count($pne))
	$mostVisitedProductEvent : ProductNavigationEvent() from accumulate ($pne : ProductNavigationEvent() 
														      over window:time(1h)
														      mostVisited($pne))
then
	if ($m.intValue() > 0) {
		channels["statisticsHourly"].send("The most visited product in the last hour was "+ $mostVisitedProductEvent.getProductName() + "(" +$mostVisitedProductEvent.getProductId() + ")." );
	}
end


rule "Report no visited products in the last day."
timer (cron:0 0 0 */1 * ?)
no-loop
when
	not (ProductNavigationEvent() over window:time(24h))
then
	channels["statisticsDaily"].send("No product was visited in the last day." );
end

rule "Report most visited product in the last day if any."
no-loop
timer (cron:0 0 0 */1 * ?)
when
	exists (ProductNavigationEvent() over window:time(24h))
	$m: Number() from accumulate ($pne : ProductNavigationEvent() over window:time(24h), count($pne))
	$mostVisitedProductEvent : ProductNavigationEvent() from accumulate ($pne : ProductNavigationEvent() 
														      over window:time(24h)
														      mostVisited($pne))
then
	if ($m.intValue() > 0) {
		channels["statisticsDaily"].send("The most visited product in the last day was "+ $mostVisitedProductEvent.getProductName() + "(" +$mostVisitedProductEvent.getProductId() + ")." );
	}
end


