package cz.ixi.fusionweb.drools.rules;

rule "Create notification if there is visited, but not bought product."
no-loop
when
	ProductNavigationEvent($productId: productId, $productName: productName) 
	not ProductVisitedButNotBoughtReported(productId == $productId) over window:time(1h)
	$visited: Number() from accumulate( $pne: ProductNavigationEvent(productId == $productId) over window:time(1h),  count( $pne ) )
	Number( $visited - intValue >= 100 ) from accumulate( $pbe: ProductBoughtEvent(productId == $productId) over window:time(1h),  count( $pbe ) )

then
	insert(new ProductVisitedButNotBoughtReported($productId));
	channels["notificationsGeneral"].send(new Notification(NotificationSeverity.SEVERE, new Date(), 
	"In the last hour was product "+ $productId +":" +$productName + " been visited many times, but not bought a lot." ));
end

rule "Product Visited But Not Bought Reported"
when
  ProductVisitedButNotBoughtReported() over window:time(1h)
then
end

rule "Report most visited product in the last hour if any."
timer (cron:0 0 */1 * * ?)
when
	$mostVisitedProductEventHourly : ProductNavigationEvent() from accumulate ($hourlyPNEvents : ProductNavigationEvent() over window:time(1h),
														    				   mostVisitedProduct($hourlyPNEvents))
then
	if ($mostVisitedProductEventHourly.getProductId() != -1) {
		channels["statisticsHourly"].send("The most visited product in the last hour was "+ $mostVisitedProductEventHourly.getProductName() + "(" +$mostVisitedProductEventHourly.getProductId() + ")." );
	} else {
		channels["statisticsHourly"].send("No product was visited in the last hour." );
	}
end


rule "Report most visited product in the last day if any."
timer (cron:0 0 0 */1 * ?)
when
	$mostVisitedProductEventDaily : ProductNavigationEvent() from accumulate ($dailyPNEvents : ProductNavigationEvent() over window:time(24h),
														      				  mostVisitedProduct($dailyPNEvents))
then
	if ($mostVisitedProductEventDaily.getProductId() != -1) {
		channels["statisticsDaily"].send("The most visited product in the last day was "+ $mostVisitedProductEventDaily.getProductName() + "(" +$mostVisitedProductEventDaily.getProductId() + ")." );
	} else {
		channels["statisticsDaily"].send("No product was visited in the last day." );
	}
end


