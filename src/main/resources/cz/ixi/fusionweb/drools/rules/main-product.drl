package cz.ixi.fusionweb.drools.rules;

rule "Choose first main product at the start."
when
	not CurrentMainProduct()
then
	insert(new CurrentMainProduct(30));
	channels["defaultLayout"].send(30);
end

rule "Change main product if there is a better one."
timer (cron:0 0 */1 * * ?)
no-loop
when
	exists (ProductNavigationEvent() over window:time(1h))
	$current : CurrentMainProduct()
	$mostVisitedProductEvent : ProductNavigationEvent() from accumulate ($PNEventMain : ProductNavigationEvent() 
														      over window:time(1h) 
														      mostVisitedProduct($PNEventMain))
 	eval ($current.getId() != $mostVisitedProductEvent.getProductId())	 
then
	modify ($current) {
		setId($mostVisitedProductEvent.getProductId());
	}
	channels["defaultLayout"].send($mostVisitedProductEvent.getProductId());
end

